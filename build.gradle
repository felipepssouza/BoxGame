group 'com.boxgame'
version '1.0'

buildscript {
    ext {
        springBootVersion = '1.4.0.RELEASE'
    }
    repositories {
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "http://repo.spring.io/milestone" }
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'

dependencies {
    compile project(":usecase")
}

jar {
    baseName = 'BoxGame'
    version = '1.0'
    manifest {
        attributes 'Main-Class': 'br.com.felipe.Application'
    }
}


/**
 * Confs to all projects
 */
allprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'jacoco'
    apply plugin: 'findbugs'

    sourceCompatibility = 1.8
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "http://repo1.maven.org/maven2/" }
    }
    dependencies {
        compile group: 'org.codehaus.sonar', name: 'sonar-plugin-api', version: '5.1'
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }

}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath)
            .include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        html.enabled true
        html.destination "${buildDir}/reports/jacoco/report.html"
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}
